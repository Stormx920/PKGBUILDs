pkgname="dwm-watermelon"
_pkgname=dwm
pkgver=6.4.release.4.r0.ge27347c
pkgrel=1
pkgdesc="Tyrone WatermelonÂ´s dwm build"
arch=('i686' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/TyroneWatermelon420/dwm"
license=('MIT')
provides=(dwm)
conflicts=(dwm)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')

source=("$_pkgname::git+https://github.com/TyroneWatermelon420/dwm")

sha256sums=('SKIP')

pkgver(){
  cd $_pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
}

