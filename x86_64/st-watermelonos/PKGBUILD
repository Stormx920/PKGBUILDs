# Maintainer: Tyrone Watermelon <tyronewatermlon@tutanota.com>
pkgname=st-watermelonos
pkgver=0.9.r15.6a54e5f
pkgrel=1
pkgdesc="Patched build of st for watermelonOS."
arch=('x86_64')
url="https://github.com/TyroneWatermelon420/st-watermelonos.git"
license=('MIT')
groups=()
depends=()
makedepends=(make)
checkdepends=()
optdepends=(ttf-hack-nerd)
provides=(st)
conflicts=(st)
replaces=()
backup=()
options=()
source=("git+$url")
noextract=()
md5sums=('SKIP')
validpgpkeys=()

pkgver() {
	cd "$srcdir/${pkgname}"
	printf "%s.r%s.%s" "$(awk '/^VERSION =/ {print $3}' config.mk)" \
		"$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd $srcdir/${pkgname}
	# skip terminfo which conflicts with ncurses
	sed -i '/tic /d' Makefile
}

build() {
  cd "${pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${pkgname}"  
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

